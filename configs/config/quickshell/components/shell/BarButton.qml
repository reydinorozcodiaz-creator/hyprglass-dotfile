pragma ComponentBehavior: Bound
import QtQuick
import qs.config

Rectangle {
    id: root

    property bool active: false
    property var contentItem: null
    readonly property bool hovered: mouseArea.containsMouse

    signal clicked
    signal rightClicked

    implicitWidth: (contentItem?.implicitWidth ?? 0) + (Config.padding * 2)
    implicitHeight: Config.barHeight - 6
    radius: height / 2

    color: (active || hovered) ? Config.surface1Color : Qt.alpha(Config.surface1Color, 0)

    Behavior on color {
        ColorAnimation {
            duration: Config.animDuration
        }
    }

    function attachContentItem() {
        if (!contentItem || contentItem.parent === contentHost)
            return;

        contentItem.parent = contentHost;
    }

    onContentItemChanged: attachContentItem()
    Component.onCompleted: attachContentItem()

    MouseArea {
        id: mouseArea
        anchors.fill: parent
        hoverEnabled: true
        cursorShape: Qt.PointingHandCursor
        acceptedButtons: Qt.LeftButton | Qt.RightButton

        onClicked: mouse => {
            if (mouse.button === Qt.RightButton)
                root.rightClicked();
            else
                root.clicked();
        }
    }

    Item {
        id: contentHost
        anchors.centerIn: parent
    }
}
